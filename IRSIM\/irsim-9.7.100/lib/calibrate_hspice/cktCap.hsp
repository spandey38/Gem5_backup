* spice test file for generating capacitance for .prm files.


.param Sup=5
.lib	'opConditions.lib' tt

.GLOBAL Vdd Gnd
Vdd	Vdd	Gnd	DC	'Supply'
.param sc=1u
.opt	scale=1u

.param	area=10000	perim=10000	cur=100u

Madn adn Gnd Gnd Gnd NMOS L=3	W=1	
+	AD='area'		PD=0
Iadn	Vdd	adn	DC	'cur'
Madp adp Vdd Vdd Vdd PMOS L=3	W=1	
+	AD='area'		PD=0
Iadp	adp	Gnd	DC	'cur'
Mpdn pdn Gnd Gnd Gnd NMOS L=3	W=1	
+	AD=0		PD='perim'
Ipdn	Vdd	pdn	DC	'cur'
Mpdp pdp Vdd Vdd Vdd PMOS L=3	W=1	
+	AD=0		PD='perim'
Ipdp	pdp	Gnd	DC	'cur'


.ic V(adn)=0 V(adp)='Supply'
.ic V(pdn)=0 V(pdp)='Supply'

.opt nomod post autostop
.tran 0.01ns 500ns


.measure	tran	tadn	when	v(adn)='Supply' rise=1
.measure	tran	tpdn	when	v(pdn)='Supply' rise=1
.measure	tran	tadp	when	v(adp)='0' fall=1
.measure	tran	tpdp	when	v(pdp)='0' fall=1
* pf/um pf/um^2
.measure	tran	cpdn	param='tpdn*cur/Supply/perim/sc*1e12/1e6'
.measure	tran	cadn	param='tadn*cur/Supply/area/sc/sc*1e12/1e6/1e6'
.measure	tran	cpdp	param='tpdp*cur/Supply/perim/sc*1e12/1e6'
.measure	tran	cadp	param='tadp*cur/Supply/area/sc/sc*1e12/1e6/1e6'

.end
